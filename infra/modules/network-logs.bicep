// Log Analytics workspace + Container Apps managed environment.
param location string
param baseName string

resource logs 'Microsoft.OperationalInsights/workspaces@2023-09-01' = {
  name: 'log-${baseName}'
  location: location
  properties: {
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 30
  }
}

resource environment 'Microsoft.App/managedEnvironments@2024-03-01' = {
  name: 'cae-${baseName}'
  location: location
  properties: {
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: logs.properties.customerId
        sharedKey: logs.listKeys().primarySharedKey
      }
    }
  }
}

output environmentId string = environment.id
// The app FQDN is '<appName>.<defaultDomain>', known before the app exists,
// so KC_HOSTNAME can be set in the same deployment.
output defaultDomain string = environment.properties.defaultDomain
